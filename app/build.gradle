plugins {
    id("com.android.application")
    id("kotlin-android")
    id("com.google.dagger.hilt.android")
    id("kotlin-kapt")
}
apply from: "$rootDir/buildSrc/scripts/common_config.gradle"

android {
    namespace 'net.davidcrotty.itemcatalogue'

    defaultConfig {
        applicationId = "net.davidcrotty.itemcatalogue"
    }

    def props = new Properties()
    props.load(file("$rootProject.projectDir/gradle/keys.properties").newReader())

    buildTypes {
        debug {
            resValue "string", "dnd_tools_api_key", "$props.dndAPIKeyTest"
        }
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.debug // replace with your production signing key
            resValue "string", "dnd_tools_api_key", "$props.dndAPIKeyProduction"
        }
    }

    buildFeatures {
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.7"
    }
}

kapt {
    correctErrorTypes true
}

dependencies { handler ->
    Utilities.addModules(
            handler,
            Modules.application,
            Modules.feature,
            Modules.androidCommon,
            Modules.jvmTest,
            Modules.instrumentedTest
    )

    kapt "com.google.dagger:hilt-compiler:${Versions.hilt}"

    implementation project(":feature:detailscreen")
    implementation project(":domain:items")
    implementation project(":data")
    implementation project(":theme")

    testImplementation project(":core:unittest")

    androidTestImplementation platform("androidx.compose:compose-bom:2023.05.01")
}